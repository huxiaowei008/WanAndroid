apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


def vName = config.version_name
def projectName = 'FrameCore'
def gitUrl = 'https://github.com/huxiaowei008/WanAndroid.git'   // Git仓库的url
def siteUrl = 'https://github.com/huxiaowei008/WanAndroid'   // 项目的主页

//项目引用的版本号，比如compile 'com.yanzhenjie:andserver:1.0.1'中的1.0.1就是这里配置的。
version = vName//指定版本号
//唯一包名，比如compile 'com.yanzhenjie:andserver:1.0.1'中的com.yanzhenjie就是这里配置的。
group = "com.hxw.mycore"// 这里是groupId ,必须填写  一般填你唯一的包名

//这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，
//所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    //读取 local.properties 文件里面的 bintray.user
    user = properties.getProperty("bintray.user")
    //读取 local.properties 文件里面的 bintray.apikey
    key = properties.getProperty("bintray.apikey")

    pkg {
        userOrg = 'huxiaowei008'//发布到JCenter的组织
        repo = 'maven'//发布到JCenter上的仓库名称
        name = projectName//发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
        version {
            name = vName
            desc = projectName //描述
            released = new Date()
            vcsTag = 'v' + vName
        }
    }
    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name projectName // 可选，项目名称
//                description 'A test'// 可选，项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'huxiaowei008' // 开发者的id,填写bintray或者github的用户名。
                        name 'huxiaowei' // 开发者名字。
                        email 'huxiaowei008@126.com' // 开发者邮箱。
                    }
                }
                // SCM,这里不需要修改。
                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}

// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

}
// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

//把javadoc的编码方式变为UTF-8
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}






